{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "4a8be717",
   "metadata": {},
   "source": [
    "# Firmware para <span style=\"color:#F37263\">Arduino M1</span> (Comunicación serie entre PC y Arduino)\n",
    "\n",
    "La siguiente notebook es parte del firmware de la versión *FMR-001 Rev A* del <span style=\"color:#F37263\">**Arduino M1**</span>.\n",
    "\n",
    "Sirve para,\n",
    "- Comunicarse con la PC que procesa y clasifica la señal de EEG.\n",
    "- Enviar los comandos de movimiento y recibir el estado del robot desde <span style=\"color:#008a3e\">**Arduino M3**</span>.\n",
    "\n",
    "**NOTA:** El código presentado debajo será editado para gregarle funcionalidades."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "90ff03c0",
   "metadata": {},
   "source": [
    "### Declaración de variables globales\n",
    "\n",
    "Lo primero que hacemos incluir algunas librerias y declarar variables globales. Recordar que las variables globales pueden ser accedidas desde cualquier parte del código. Por lo tanto debemos ser cuidadosos/as cuando estemos generando cambios en estas.\n",
    "\n",
    "Veamos la primera parte del código.\n",
    "\n",
    "```c++\n",
    "\n",
    "/******************************************************************\n",
    "            VERSIÓN FMR-001 Rev A\n",
    "******************************************************************/\n",
    "\n",
    "#include \"definiciones.h\"\n",
    "#include \"inicializaciones.h\"\n",
    "#include \"funciones.h\"\n",
    "\n",
    "/******************************************************************\n",
    "  Declaración de variables para comunicación\n",
    "/******************************************************************/\n",
    "\n",
    "char inBuffDataFromPC = 3;\n",
    "unsigned char incDataFromPC[3]; //variable para almacenar datos provenientes de la PC\n",
    "char bufferIndex = 0;\n",
    "bool sendDataFlag = 0;\n",
    "bool newMessage = false;\n",
    "\n",
    "unsigned char internalStatus[4]; //variable para enviar datos a la PC\n",
    "char internalStatusBuff = 4;\n",
    "\n",
    "unsigned char outputDataToRobot[4]; //variable para enviar datos al robot\n",
    "char buffOutDataRobotSize = 4;\n",
    "char buffOutDataRobotIndex = 0;\n",
    "\n",
    "unsigned char incDataFromRobot[4]; //variable para recibir datos del robot\n",
    "char incDataFromRobotSize = 4;\n",
    "char incDataFromRobotIndex = 0;\n",
    "\n",
    "/******************************************************************\n",
    "  Variables para el control de flujo de programa\n",
    "******************************************************************/\n",
    "char sessionState = 0; //Sesión sin iniciar\n",
    "char LEDVerde = 12;\n",
    "char LEDTesteo = 13; //led de testeo\n",
    "\n",
    "/******************************************************************\n",
    "  Declaración de variables para control de estímulos\n",
    "******************************************************************/\n",
    "\n",
    "int frecTimer = 5000; //en Hz. Frecuencia de interrupción del timer.\n",
    "\n",
    "//estímulo izquierdo\n",
    "char estimIzq = 11;\n",
    "bool estimIzqON = 0;//Esado que define si el LED se apgará o prenderá.\n",
    "int frecEstimIzq = 11;\n",
    "int acumEstimIzq = 0;\n",
    "const int estimIzqMaxValue = (1/float(frecEstimIzq))*frecTimer;\n",
    "\n",
    "//estímulo derecho\n",
    "char estimDer = 7;\n",
    "bool estimDerON = 0;//Esado que define si el LED se apgará o prenderá.\n",
    "int frecEstimDer = 11;\n",
    "int acumEstimDer = 0;\n",
    "const int estimDerMaxValue = (1/float(frecEstimDer))*frecTimer;\n",
    "\n",
    "/*Implementar lo siguiente\n",
    "//estímulo adelante\n",
    "//estímulo atrás\n",
    "*/\n",
    "\n",
    "char stimuli = ON; //variable golbal para control de estímulos\n",
    "int acumuladorStimuliON = 0;\n",
    "int trialNumber = 1;\n",
    "\n",
    "/******************************************************************\n",
    "  Variables control de movimiento\n",
    "******************************************************************/\n",
    "char movimiento = 0; //Robot en STOP\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "83cc7e1e",
   "metadata": {},
   "source": [
    "### Función setup()\n",
    "\n",
    "Como sabemos, la función *setup()* de Arduino nos permite configurar e inicializar variables, puertos, interrupciones, entre otras.\n",
    "\n",
    "Se ejecuta **una sola vez** al iniciar el programa.\n",
    "\n",
    "Para la versión *FMR-001 Rev A* lo implementado es,\n",
    "\n",
    "```c++\n",
    "//FUNCION SETUP\n",
    "void setup()\n",
    "{\n",
    "  noInterrupts();//Deshabilito todas las interrupciones\n",
    "  pinMode(estimIzq,OUTPUT);\n",
    "  pinMode(estimDer,OUTPUT);\n",
    "  pinMode(LEDVerde,OUTPUT);\n",
    "  pinMode(LEDTesteo,OUTPUT);\n",
    "  iniTimer0(frecTimer); //inicio timer 0\n",
    "  Serial.begin(19200); //iniciamos comunicación serie\n",
    "\n",
    "  interrupts();//Habilito las interrupciones\n",
    "}\n",
    "```\n",
    "\n",
    "Podemos ver que se inicializan los pines de los estímulos como salida. Se invoca a la función *iniTimer0()* la cual esta dentro de la libreria *funciones.h*."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "58ec6905",
   "metadata": {},
   "source": [
    "### Función loop()\n",
    "\n",
    "Vemos que la función *loop()* no tiene nada dentro.\n",
    "\n",
    "```c++\n",
    "void loop(){}\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "6c6fd867",
   "metadata": {},
   "source": [
    "### Interrupción UART: Función para recibir y enviar datos por la UART\n",
    "\n",
    "Cada vez que nos llega un dato por la UART del micronctrolador se genera una interrupción, la cual luego de que el microcontrolador ejecuta su última instrucción en curso y dependiendo de las prioridades de interrupción seteadas, la siguiente instrucción ejecutará la función *serialEvent()*. Esta función nos permite recibir y enviar mensajes por la UART.\n",
    "\n",
    "Su implementación es la siguiente.\n",
    "\n",
    "```c++\n",
    "void serialEvent()\n",
    "{\n",
    "if (Serial.available() > 0) \n",
    "  {\n",
    "    char val = char(Serial.read()) - '0';\n",
    "    checkMessage(val); //chequeamos mensaje entrante        \n",
    "    for(int index = 0; index < internalStatusBuff; index++) //enviamos estado \n",
    "      {Serial.write(internalStatus[index]);}\n",
    "      Serial.write(\"\\n\");\n",
    "  }\n",
    "};\n",
    "```\n",
    "\n",
    "Es **importante** notar que con cada nuevo byte entrante se invoca a la función *checkMessage()*. Veremos más adelante de que se trata la misma.\n",
    "\n",
    "Por otro lado, una vez chequeado el mensaje se envía un set de bytes con el **estado interno** de nuestro Arduino a la PC. Como vemos, siempre que llega un nuevo Byte se envía el estado interno.\n",
    "\n",
    "<span style=\"color:red\">**Importante:**</span> La actualización del estado interno de nuestro <span style=\"color:#F37263\">**Arduino M1**</span>, el envío de un comando al <span style=\"color:#008a3e\">**Arduino M3**</span>, entre otras acciones, estarán actualizadas con la llegada un nuevo Byte desde la PC, es decir, con cada inerrupción de la UART."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "09fb0af3",
   "metadata": {},
   "source": [
    "### Chequeando mensaje proveniente de la PC\n",
    "\n",
    "Como vimos, con cada nuevo byte entrante desde la PC invocamos a la función *checkMessage()*. Su implementación para la versión actual del firmware es,\n",
    "\n",
    "```c++\n",
    "void checkMessage(char val)\n",
    "{\n",
    "    \n",
    "  incDataFromPC[bufferIndex] = val;\n",
    "  switch(bufferIndex)\n",
    "  {\n",
    "    case 0:\n",
    "      if (incDataFromPC[bufferIndex] == 1) sessionState = SESSION_RUNNING;\n",
    "      else sessionState = SESSION_STOP;\n",
    "      digitalWrite(LEDTesteo,0);\n",
    "      //else sessionState = STOP;\n",
    "      break;\n",
    "    case 1:\n",
    "      if (incDataFromPC[bufferIndex] == 1) stimuli = ON;\n",
    "      else stimuli = OFF;\n",
    "      //else sessionState = STOP;\n",
    "      break;\n",
    "\n",
    "    case 2: //indica hacia donde se debe mover el vehículo\n",
    "      char comando = incDataFromPC[bufferIndex];\n",
    "      sendCommand(comando); //Es mejor hacer sendCommand(incDataFromPC[bufferIndex])\n",
    "      break;          \n",
    "  }\n",
    "    \n",
    "  bufferIndex++;\n",
    "  if (bufferIndex >= inBuffDataFromPC) bufferIndex = 0;\n",
    "};\n",
    "```\n",
    "\n",
    "Como vemos, el byte *char val* pasado como parámetro es almacenado dentro del arreglo de chars llamado *incDataFromPC* en la posición *bufferIndex*. Esto es **importante** ya que cada posición dentro del arreglo *incDataFromPC* posee un rol importante, recordar del código de la clase *ArduinoCommunication* de Python que el mensaje enviado desde la PC al <span style=\"color:#F37263\">**Arduino M1**</span> esta tiene la forma,\n",
    "\n",
    "\n",
    "```python\n",
    "    self.systemControl = [self.sessionStatus,\n",
    "                         self.stimuliStatus,\n",
    "                         self.moveOrder]\n",
    "```\n",
    "\n",
    "Considerando lo anterior, en el caso de que *bufferIndex* tome el valor *2* estaremos almacenando en *incDataFromPC* la orden de movimiento para enviar al robot. Por lo tanto se invoca la función *sendCommand()* la cual enviará el comando de movimiento al <span style=\"color:#008a3e\">**Arduino M3**</span> a través del Bluettoth para que el <span style=\"color:#008a3e\">**Arduino M3**</span> controle el vehículo.\n",
    "\n",
    "**Nota:** La función *sendCommand()* aún no esta implementada."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "85d6bba5",
   "metadata": {},
   "source": [
    "### Interrupción timer0\n",
    "\n",
    "Para la versión de este firmware se configuro el *timer0* para interrumpir cada *0.2 milisegunds* (5000Hz).\n",
    "\n",
    "Es <span style=\"color:red\">**sumamente importante**</span> que el *timer0* produzca una interrupción en la frecuencia correcta. Es decir, cualquier cambio en la frecuencia de interrupción debe reflejarse en la variable *frecTimer* y por supuesto, en los registros del *timer 0* del micro. La configuración de los registros del *timer 0* del microcontrolador pueden verse dentro de la función *iniTimer0()* dentro de la libreria *inicializaciones.h*.\n",
    "\n",
    "Por otro lado, siempre es recomendable chequear con un osciloscopio si se ha configurado correctamente un timer, generando una señal cuadrada a la salida de algún pin del microcontrolador que cambie de estado alto/bajo con cada interrupción del timer. Luego se mide el tiempo en que la señal esta en estado alto (o bajo).\n",
    "\n",
    "##### Acciones al producirse una interrupción\n",
    "\n",
    "Cada *0.2 ms* se ejecuta la siguiente rutina de código,\n",
    "\n",
    "```c++\n",
    "ISR(TIMER0_COMPA_vect)//Rutina interrupción Timer0.\n",
    "{\n",
    "  if(sessionState == SESSION_RUNNING) stimuliControl(); //Si la sesión comenzó, empezamos a generar los estímulos  \n",
    "  else\n",
    "  {\n",
    "    //apago estímulos\n",
    "    digitalWrite(estimIzq,0);\n",
    "    digitalWrite(estimDer,0);\n",
    "    digitalWrite(LEDTesteo,1);\n",
    "  }\n",
    "};\n",
    "```\n",
    "\n",
    "Su implementación es muy facil, vemos que si *if(sessionState == SESSION_RUNNING)* entonces invocamos la función *stimuliControl()*. Si lo anterior no se cumple, es de esperar que la sesión este frenada y por lo tanto se apagan los estímulos."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "54e822a1",
   "metadata": {},
   "source": [
    "### Controlando estímulos - Función *stimuliControl()*\n",
    "\n",
    "Para el control de estímulos se implemento la función *stimuliControl()*.\n",
    "\n",
    "```c++\n",
    "void stimuliControl()\n",
    "{\n",
    "  acumuladorStimuliON++;\n",
    "  switch(stimuli)\n",
    "  {\n",
    "    case ON:\n",
    "    //control estímulo izquierdo\n",
    "      if (++acumEstimIzq >= estimIzqMaxValue)\n",
    "      {\n",
    "        estimIzqON = !estimIzqON;\n",
    "        digitalWrite(estimIzq,estimIzqON);\n",
    "        acumEstimIzq = 0; \n",
    "      } \n",
    "\n",
    "    //control estímulo derecho\n",
    "      if (++acumEstimDer >= estimDerMaxValue)\n",
    "      {\n",
    "        estimDerON = !estimDerON;\n",
    "        digitalWrite(estimDer,estimDerON);\n",
    "        acumEstimDer = 0; \n",
    "      } \n",
    "      break;\n",
    "\n",
    "    case OFF:\n",
    "      {\n",
    "        //Apagamos estímulos y reiniciamos contadores\n",
    "        digitalWrite(estimIzq,0);\n",
    "        acumEstimIzq = 0;\n",
    "        digitalWrite(estimDer,0);\n",
    "        acumEstimDer = 0;\n",
    "        trialNumber++; //Sumamos un nuevo trial\n",
    "        acumuladorStimuliON = 0; //reiniciamos acumulador para temporizar cada trial\n",
    "        acumEstimIzq = 0;\n",
    "        acumEstimDer = 0;\n",
    "      } \n",
    "      break;\n",
    "  }\n",
    "}\n",
    "```\n",
    "\n",
    "Es importante notar que:\n",
    "- Si la variable *stimuli* es *ON* indica que estamos en la parte del trial que **debemos** estimular. Por lo tanto, los estímulos (LEDs) se prenderán y apagaran según la frecuencia con la que hayan sido configuradas.\n",
    "- Si la variable *stimuli* es *OFF* estamos en la parte del trial que **no debemos** estimular. Por lo tanto, apagamos los estímulos y reiniciamos variables de control a sus valores iniciales."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "f4d43b71",
   "metadata": {},
   "source": [
    "### <span style=\"color:#3363b0\">Comentarios finales </span> \n",
    "\n",
    "El código hasta aquí presentado corresponde a la versión de firmware *FMR-001 Rev A*. Este código deberá crecer y agregar funcionalidades, además, cada equipo tiene vehículos diferentes y cantidades de comandos diferentes, por lo tanto, el código de este firmware será diferente para cada caso, no obstante, la lógica del control de flujo y de procesos **es la misma** para cada equipo, lo que cambia es la cantidad de datos que se envían y reciben."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "19804e51",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python [conda env:taller4-BCIC]",
   "language": "python",
   "name": "conda-env-taller4-BCIC-py"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.9.5"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
